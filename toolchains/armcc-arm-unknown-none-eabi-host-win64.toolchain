set(CMAKE_SYSTEM_NAME Generic)
set(CMAKE_SYSTEM_PROCESSOR ARM)
set(CMAKE_SYSTEM_VERSION "5")

set(TOOLCHAIN_PATH $ENV{ARMCC_PATH})

set(TOOLCHAIN_PATH_BIN ${TOOLCHAIN_PATH}/bin64)
set(TOOLCHAIN_PATH_INC ${TOOLCHAIN_PATH}/include)
# Note that this toolchain can be used on Linux, but via translator like
# Wine. In that case we will still use .exe files.
set(EXECUTABLE_SUFFIX .exe)

set(CMAKE_C_FLAGS_INIT "-I${TOOLCHAIN_PATH_INC} --c99" CACHE INTERNAL "" FORCE)
find_program(ARM_CC NAMES armcc${EXECUTABLE_SUFFIX} PATHS ${TOOLCHAIN_PATH_BIN} REQUIRED NO_DEFAULT_PATH)
find_program(ARM_CXX NAMES armcc${EXECUTABLE_SUFFIX} PATHS ${TOOLCHAIN_PATH_BIN} REQUIRED NO_DEFAULT_PATH)
find_program(ARM_ASM NAMES armasm${EXECUTABLE_SUFFIX} PATHS ${TOOLCHAIN_PATH_BIN} REQUIRED NO_DEFAULT_PATH)
find_program(ARM_AR NAMES armar${EXECUTABLE_SUFFIX} PATHS ${TOOLCHAIN_PATH_BIN} REQUIRED NO_DEFAULT_PATH)
find_program(ARM_LINK NAMES armlink${EXECUTABLE_SUFFIX} PATHS ${TOOLCHAIN_PATH_BIN} REQUIRED NO_DEFAULT_PATH)
find_program(ARM_FROMELF NAMES fromelf${EXECUTABLE_SUFFIX} PATHS ${TOOLCHAIN_PATH_BIN} REQUIRED NO_DEFAULT_PATH)

set(CMAKE_C_COMPILER ${ARM_CC} CACHE STRING "")
set(CMAKE_C_COMPILER_ID "ARMCC" CACHE STRING "")
set(CMAKE_C_OUTPUT_EXTENSION ".o")
set(CMAKE_CXX_COMPILER ${ARM_CXX} CACHE STRING "")
set(CMAKE_CXX_COMPILER_ID "ARMCC" CACHE STRING "")
set(CMAKE_CXX_OUTPUT_EXTENSION ".o")
set(CMAKE_ASM_COMPILER ${ARM_ASM} CACHE STRING "")
set(CMAKE_ASM_COMPILER_ID "ARMCC" CACHE STRING "")
set(CMAKE_ASM_OUTPUT_EXTENSION ".o")
set(CMAKE_LINKER ${ARM_LINK} CACHE STRING "")
set(CMAKE_ARMCC_LINKER ${ARM_LINK} CACHE STRING "")
set(CMAKE_AR ${ARM_AR} CACHE STRING "")
set(CMAKE_ARMCC_AR ${ARM_AR} CACHE STRING "")
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)
set(CMAKE_FIND_ROOT_PATH ${TOOLCHAIN_PATH})
set(CMAKE_C_COMPILER_WORKS 1)
set(CMAKE_CXX_COMPILER_WORKS 1)

### AR FLAGS ###
set(CMAKE_C_ARCHIVE_CREATE "<CMAKE_AR> --create -cr <TARGET> <LINK_FLAGS> <OBJECTS>")
set(CMAKE_ASM_ARCHIVE_CREATE "<CMAKE_AR> --create -cr <TARGET> <LINK_FLAGS> <OBJECTS>")
set(CMAKE_CXX_ARCHIVE_CREATE "<CMAKE_AR> --create -cr <TARGET> <LINK_FLAGS> <OBJECTS>")
set(CMAKE_C_ARCHIVE_FINISH "")
set(CMAKE_ASM_ARCHIVE_FINISH "")
set(CMAKE_CXX_ARCHIVE_FINISH "")

### LINKER FLAGS ###
set(CMAKE_C_LIBRARY_PATH_FLAG "--userlibpath ")
set(CMAKE_CXX_LIBRARY_PATH_FLAG "--userlibpath ")
set(CMAKE_C_LINK_LIBRARY_FLAG "")
set(CMAKE_CXX_LINK_LIBRARY_FLAG "")
set(CMAKE_C_LINK_EXECUTABLE "<CMAKE_LINKER> <CMAKE_C_LINK_FLAGS> <LINK_FLAGS> <LINK_LIBRARIES> <OBJECTS> -o <TARGET> --list <TARGET_BASE>.map")
set(CMAKE_CXX_LINK_EXECUTABLE "<CMAKE_LINKER> <CMAKE_CXX_LINK_FLAGS> <LINK_FLAGS> <LINK_LIBRARIES> <OBJECTS> -o <TARGET> --list <TARGET_BASE>.map")
